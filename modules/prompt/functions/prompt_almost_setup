#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_almost_precmd {
    # Check for untracked files or updated submodules since vcs_info does not.
    if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
      branch_format="${_C[7]}%b%f%u%c${_C[4]}●%f"
    else
      branch_format="${_C[7]}%b%f%u%c"
    fi

    zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

    vcs_info 'prompt'

    if (( $+functions[python-info] )); then
      python-info
    fi

    _set_python_format
    prompt_sorin_pwd
    _set_prompt
}

function _set_python_format {
    if [[ -n ${python_info[virtualenv]} ]]; then
        _python_format=" ${_C[2]}${python_info[virtualenv]}%f${_C[4]}%B∼%b%f"
    else
        _python_format=''
    fi
}

function prompt_almost_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    # Load required functions.
    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    # Add hook for calling vcs_info before each command.
    add-zsh-hook precmd prompt_almost_precmd

    # Use extended color pallete if available.
    if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
      _C=(
        "%F{81}"  # Turquoise
        "%F{166}" # Orange
        "%F{135}" # Purple
        "%F{161}" # Hotpink
        "%F{118}" # Limegreen
        "%F{239}" # Dark grey
        "%F{91}" # Dark purple
        "%F{196}" # Red
        "%F{250}" # White
      )
    else
      _C=(
        "%F{cyan}"
        "%F{yellow}"
        "%F{magenta}"
        "%F{red}"
        "%F{green}"
        "%F{grey}"
      )
    fi

    # Formats:
    #   %b - branchname
    #   %u - unstagedstr (see below)
    #   %c - stagedstr (see below)
    #   %a - action (e.g. rebase-i)
    #   %R - repository path
    #   %S - path in the repository
    local branch_format="${_C[7]}%b%f%u%c"
    local action_format="${_C[5]}%a%f"
    local unstaged_format="${_C[2]}✖%f"
    local staged_format="${_C[5]}✔%f"

    if [[ -n "$SSH_TTY" ]]; then
        host='%M'
    else
        host='%m'
    fi

    # Set vcs_info parameters.
    zstyle ':vcs_info:*' enable bzr git hg svn
    zstyle ':vcs_info:*:prompt:*' check-for-changes true
    zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
    zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
    zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
    zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
    zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

    zstyle ':prezto:module:editor:info:keymap:primary' format '%B%(!.'"${_C[8]}"'#%f.'"${_C[2]}"'§%f)%b'
    zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%(!.'"${_C[8]}"'!%f.'"${_C[2]}"'$%f)%b'

    # # Set git-info parameters.
    # zstyle ':prezto:module:git:info' verbose 'yes'
    # zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
    # zstyle ':prezto:module:git:info:added' format ' %%B%F{2}✚%f%%b'
    # zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}⬆%f%%b'
    # zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}⬇%f%%b'
    # zstyle ':prezto:module:git:info:branch' format ' %%B%F{2}%b%f%%b'
    # zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
    # zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}✖%f%%b'
    # zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}✱%f%%b'
    # zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
    # zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}➜%f%%b'
    # zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}✭%f%%b'
    # zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
    # zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}◼%f%%b'
    # zstyle ':prezto:module:git:info:keys' format \
    #     'status' '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

    # Set python-info parameters.
    zstyle ':prezto:module:python:info:virtualenv' format '%v'

    # Define prompts.
    _set_python_format
    _set_prompt

    RPROMPT=""'${VIM:+"'"$_C[5]"'✱%f"}${vcs_info_msg_0_:+" ${vcs_info_msg_0_}"}'""'${_python_format}'
}

function _set_prompt {
    left_prompt="${_C[6]}→%f ${_C[4]}%n%f at ${_C[5]}$host%f in ${_C[1]}${_prompt_sorin_pwd}%f"

    local zero='%([BSUbfksu]|([FBK]|){*})'
    local left_width=${#${(S%%)left_prompt//$~zero/}}

    local width=$(($COLUMNS-${left_width}-2))
    local time_='${_C[6]}'

    PROMPT='
$left_prompt'"${(l:$width:)_C[6]}"'%D{%H:%M:%S}%f
${editor_info[keymap]} '

}


prompt_almost_setup "$@"
